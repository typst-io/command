plugins {
    id 'base'
}

allprojects {
    group 'io.typecraft'
    version '0.8.0'
    def moduleName = name.substring(name.indexOf('-') + 1)
    ext.registerPublish = {
        publishing {
            publications {
                create(moduleName, MavenPublication.class) {
                    from(components['java'])
                    pom {
                        name.set("${project.group}:${project.name}")
                        description.set("Pure, functional command line parser.")
                        url.set("https://github.com/typecraft-io/command")
                        licenses {
                            license {
                                name.set("The GNU General Public License, Version 3.0")
                                url.set("https://www.gnu.org/licenses/gpl-3.0.txt")
                            }
                        }
                        developers {
                            developer {
                                id.set("entrypointkr")
                                name.set("Junhyung Im")
                                email.set("entrypointkr@gmail.com")
                            }
                        }
                        scm {
                            connection.set("scm:git:git://github.com/typecraft-io/command.git")
                            developerConnection.set("scm:git:ssh://github.com:typecraft-io/command.git")
                            url.set("https://github.com/typecraft-io/command/tree/master")
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = "sonatypeReleases"
                    url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                    credentials {
                        username = findProperty("ossrhUsername")?.toString()
                        password = findProperty("ossrhPassword")?.toString()
                    }
                }
                maven {
                    name = "sonatypeSnapshots"
                    url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                    credentials {
                        username = findProperty("ossrhUsername")?.toString()
                        password = findProperty("ossrhPassword")?.toString()
                    }
                }
            }
        }
        signing {
            sign(publishing.publications[moduleName])
        }
        java {
            withSourcesJar()
            withJavadocJar()
        }
        tasks.javadoc {
            options.encoding = 'UTF-8'
        }
    }
}
